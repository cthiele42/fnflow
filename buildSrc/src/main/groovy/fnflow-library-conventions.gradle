plugins {
    id 'fnflow-java-conventions'
    id 'maven-publish'
    id 'org.jreleaser'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

jar {
    archiveClassifier=''
    enabled = true
}

bootJar {
    enabled = false
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
    publications {
        maven(MavenPublication) {
            versionMapping {
                allVariants {
                    fromResolutionResult()
                }
            }
            from(components.java)

            pom {
                name = '{{projectName}}'
                description = '{{projectDescription}}'
                url = 'https://github.com/cthiele42/fnflow'
                inceptionYear = '2025'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'cthiele42'
                        name = 'Claas Thiele'
                    }
                    developer {
                        id = 'safairon'
                        name = 'Sajjad Safaeian'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/cthiele42/fnflow.git'
                    developerConnection = 'scm:git:ssh://github.com/cthiele42/fnflow.git'
                    url = 'http://github.com/cthiele42/fnflow'
                }
            }
        }
    }
}

jreleaser {
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                    retryDelay = 30
                    maxRetries = 60
                }
            }
        }
    }
    release {
        github {
            tagName = '{{projectName}}-{{projectVersion}}'
        }
    }
}
