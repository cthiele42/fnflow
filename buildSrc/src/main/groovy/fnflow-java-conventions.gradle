plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
}

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

repositories {
    mavenCentral()
}

idea {
    module {
        downloadSources = true
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

def getVersionSuffix() {
    def suffix = "-UNKNOWN"
    try {
        def workingDir = new File("${project.projectDir}")
        def result = 'git rev-parse --abbrev-ref HEAD'.execute(null, workingDir)
        result.waitFor()
        if (result.exitValue() == 0) {
            suffix = result.text.trim()
            if (suffix == 'main') {
                suffix = ''
            } else {
                suffix = '-BRANCH_' + suffix.find( /^\d+/ )
            }
        }
    } catch (e) {
    }
    return suffix
}

ext {
    versionSuffix = getVersionSuffix()
}